apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

def STRING = "String"
def BASE_URL = "BASE_URL"
def APP_ID = "APP_ID"

Properties props = new Properties()
props.load(new FileInputStream(file("gradle.properties")))
def base_address = props.getProperty("base_url").toString()
def app_id = props.getProperty("app_id").toString()

android {
    compileSdkVersion project.android_sdk_version
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "koshur.kashmirievents"
        minSdkVersion 22
        targetSdkVersion project.android_sdk_version
        versionCode 5
        versionName "1.0.03"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField STRING, BASE_URL, base_address
        buildConfigField STRING, APP_ID, app_id
    }

    signingConfigs {
        release {
            storeFile project.KEYSTORE_STORE_FILE
            storePassword project.KEYSTORE_STORE_PASSWORD
            keyAlias project.KEYSTORE_KEY_ALIAS
            keyPassword project.KEYSTORE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "@string/app_name_debug"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "@string/app_name_release"
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'com.google.android.material:material:1.2.1'

    /*Room */
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    /*RxJava2*/
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava2"

    implementation 'com.google.android.gms:play-services-auth:18.1.0'
    implementation 'pub.devrel:easypermissions:0.1.5'
    implementation('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    // calendar view used in app
    implementation "com.github.kizitonwose:CalendarView:0.4.5"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    // android architecture component
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_Logging_Version"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'

    // Data binding collection adapter
    implementation "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter:4.0.0"
    implementation "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:4.0.0"

    // Timber for Logging
    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation 'com.google.firebase:firebase-analytics:17.6.0'
}